<message message-id="A5473E10FAF5D311BA83009027DCD8EBCAC35F@mail.cm-net.co.uk.20.172.in-addr.arpa" list="org.apache.perl.modperl" id="24b657mr4d3cr5pt" type="general" date="2000-12-08T17:10:57Z" year="2000-01-01" year-month="2000-12-01" year-month-day="2000-12-08" thread-id="24b657mr4d3cr5pt"><headers><envelope-from-line>From Rufus.Cable@icollector.com  Fri Dec  8 17:05:41 2000</envelope-from-line><from personal="Rufus.Cable@icollector.com" address="Rufus.Cable@icollector.com">Rufus.Cable@icollector.com</from><to personal="modperl@apache.org" address="modperl@apache.org">modperl@apache.org</to><subject normal="RFC: Apache::Thumbnail - Generate image thumbnails on the fly">RFC: Apache::Thumbnail - Generate image thumbnails on the fly</subject><return-path>Rufus.Cable@icollector.com</return-path><mailing-list>contact modperl-help@apache.org; run by ezmlm</mailing-list><delivered-to>mailing list modperl@apache.org</delivered-to><received>(qmail 58809 invoked from network); 8 Dec 2000 17:05:41 -0000</received><received>from users.cm-net.co.uk (HELO lonnt03.cm-net.co.uk) (195.152.102.5)
  by locus.apache.org with SMTP; 8 Dec 2000 17:05:41 -0000</received><received>by mail.cm-net.co.uk.20.172.in-addr.arpa with Internet Mail Service (5.5.2650.21)
	id &lt;YH9FCM1A&gt;; Fri, 8 Dec 2000 17:10:58 -0000</received><message-id>A5473E10FAF5D311BA83009027DCD8EBCAC35F@mail.cm-net.co.uk.20.172.in-addr.arpa</message-id><reply-to>Rufus.Cable@icollector.com, ruf@rcable.co.uk</reply-to><date>Fri, 8 Dec 2000 17:10:57 -0000 </date><mime-version>1.0</mime-version><x-mailer>Internet Mail Service (5.5.2650.21)</x-mailer><content-type>multipart/mixed;
	boundary="----_=_NextPart_000_01C06139.D4D96120"</content-type><x-spam-rating>locus.apache.org 1.6.2 0/1000/N</x-spam-rating></headers><attachments><attachment type="application/octet-stream" file="Thumbnail.pm" size="3847" uri="/24b657mr4d3cr5pt/attach01/Thumbnail.pm" extension="pm"><page page="1"><attachpara>package Apache::Thumbnail;
use Apache::Constants qw(:common);
use Image::Magick ();
use File::Path ();

use strict;

use vars qw($VERSION);

$VERSION = '0.5';

sub handler {
	my $r = shift;

	my $root = $r-&gt;document_root();
	my $dest = $r-&gt;dir_config('Thumbnail_Directory') || '/thumbnails';
	my $geom = $r-&gt;dir_config('Thumbnail_Geometry') || '100x100';
	my $img = $r-&gt;filename().$r-&gt;path_info();
	$img =~ s/^$root\/[^\/]+//;

	# get thumbnail name
	my $thumb = $img;

	# final filenames
	my $srcFile = "$root$img";
	my $destFile = "$root$dest$thumb";

	# already cached?
	if (-f $destFile and -r _) {
		if (-C _ &lt; -C $srcFile) {
			$r-&gt;internal_redirect("$dest$thumb");
			return OK;
		}
	}
	
	# is source image there?
	if (-s "$srcFile") {
		# yes, create thumbnail
		my $e;
		my $i = new Image::Magick;
		# read source
		$e = $i-&gt;Read("$srcFile");
		if ($e) {
			$r-&gt;log_error("Couldn't read '$srcFile': $e");
			return SERVER_ERROR;
		}
		# scale it
		$e = $i-&gt;Scale(geometry =&gt; $geom);
		if ($e) {
			$r-&gt;log_error("Couldn't scale '$srcFile': $e");
			return SERVER_ERROR;
		}
		# check directory tree is in place...
		my $path = $destFile;
		$path =~ s/\/[^\/]+$//o;
		if (!-d $path) {
			File::Path::mkpath $path ||
				($r-&gt;log_error("Couldn't create directory '$path': $!") &amp;&amp; return SERVER_ERROR);
		}
		# write thumbnail
		$e = $i-&gt;Write($destFile);
		if ($e) {
			$r-&gt;log_error("Couldn't write '$destFile': $e");
			return SERVER_ERROR;
		}
		# and send to browser
		$r-&gt;internal_redirect("$dest$thumb");
		return OK;
	}
	
	# couldn't find source image
	return NOT_FOUND;
}

1;

__END__

=head1 NAME

Apache::Thumbnail - Generate image thumbnails on the fly

=head1 SYNOPSIS

   # in httpd.conf or similar

   &lt;Location /thumbs/&gt;
      SetHandler perl-script
      PerlSetVar Thumbnail_Directory "/thumbnails"
      PerlSetVar Thumbnail_Geometry "100x100"
      PerlHandler Apache::Thumbnail
      order allow,deny
      allow from all
   &lt;/Location&gt;

   &lt;Location /smallpics/&gt;
      SetHandler perl-script
      PerlSetVar Thumbnail_Directory "/smallpics/"
      PerlSetVar Thumbnail_Geometry "200x200"
      PerlHandler Apache::Thumbnail
      order allow,deny
      allow from all
   &lt;/Location&gt;

=head1 DESCRIPTION

This module automatically generates thumbnail images on the fly, caching
them in the specified thumbnail directory in a tree structure matching
that of the source images.

To use, set up a C&lt;&lt;Location&gt;&gt; directive as above. For any image on the
web server, a corresponding thumbnail is available by prepending that
location to the path - eg.

 Image at /images/staff/john.jpg

 Thumbnail at /thumbs/images/staff/john.jpg

That's it! The first time a thumbnail is requested, it'll be generated
and cached; subsequent requests will return the cached image immediately.
If the source image is newer than the thumbnail, the thumbnail will
automatically be regenerated.

=head1 OPTIONS

=over 4

=item Thumbnail_Directory

The directory in which the finished thumbnails are cached, relative to the
webserver document root. If unspecified, the default is C&lt;/thumbnails&gt;.

 PerlSetVar Thumbnail_Directory "/thumbnails"

=item Thumbnail_Geometry

The maximum size of the thumbnail images. The proportions always remain
unchanged - see C&lt;Image::Magick::Scale()&gt; for details. If unspecified,
the default is 100x100 pixels.

 PerlSetVar Thumbnail_Geometry "100x100"

=back 4

=head1 BUGS

Doesn't currently check if the source image is smaller than the thumbnail
size, so tiny images would look nasty scaled up. Could add this, but it'd
be slightly slower, and probably not all that useful?

Another other bugs or suggestions, let me know!

=head1 PREREQUISITES

=item *
mod_perl L&lt;http://perl.apache.org/&gt;

=item *
Image::Magick L&lt;http://www.simplesystems.org/ImageMagick/&gt;

=head1 AUTHOR

Rufus Cable ruf@rcable.co.uk

=cut
</attachpara></page></attachment></attachments><normalized-references><normalized-message-id>A5473E10FAF5D311BA83009027DCD8EBCAC35F@mail.cm-net.co.uk.20.172.in-addr.arpa</normalized-message-id></normalized-references><body><para depth="0">Hi -

</para><para depth="0">Attached is an Apache module to automatically generate &amp; cache thumbnail
images. It uses Image::Magick (<url>http://www.simplesystems.org/ImageMagick/</url>)
and File::Path (CPAN) and runs quickly enough to be useful on my
Apache/mod_perl machine. It's my first proper handler, so let me know if
there are any glaring ommissions or bugs! :)

</para><para depth="0">Otherwise, if it's of use to anyone else, should I package this up and
submit it to CPAN as v1.0? Comments welcome!

</para><para depth="0">Rufus.

</para></body></message>